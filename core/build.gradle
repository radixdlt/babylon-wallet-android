plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.ksp
    alias libs.plugins.kotlin.serialization
}

android {
    namespace 'rdx.works.core'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "false"
        buildConfigField "boolean", "APP_LOCK_ENABLED", "false"
    }

    buildTypes {
        debug {
        }
        debugAlpha {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "true"
            buildConfigField "boolean", "APP_LOCK_ENABLED", "true"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "CRASH_REPORTING_ENABLED", "false"
            buildConfigField "boolean", "APP_LOCK_ENABLED", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    kotlin.compilerOptions.freeCompilerArgs.add("-Xannotation-default-target=first-only")
    flavorDimensions "version"
    productFlavors {
        full {
            dimension "version"
        }
        light {
            dimension "version"
        }
    }

    afterEvaluate {
        tasks.configureEach { task ->
            if (task.name.contains("Test") && task.name.contains("compile")) {
                kotlinOptions.freeCompilerArgs += '-Xopt-in=com.radixdlt.sargon.annotation.UsesSampleValues'
            }
        }
    }

}

// When the arculus-android-csdk submodule is not available, disable full flavor tasks
if (findProject(':arculus-android-csdk:csdknative') == null) {
    afterEvaluate {
        tasks.configureEach { task ->
            if (task.name.contains("Full")) {
                task.enabled = false
            }
        }
    }
}

dependencies {
    api libs.sargonAndroid
    if (findProject(':arculus-android-csdk:csdknative') != null) {
        fullImplementation project(path: ':arculus-android-csdk:csdknative')
    }

    implementation libs.ktorCore
    implementation libs.zxing
    implementation libs.kotlinxSerialization
    fullImplementation libs.firebaseCrashlytics
    implementation libs.immutableCollections

    // Datastore
    implementation libs.datastorePreferences

    // Hilt
    implementation libs.hilt
    ksp libs.hiltCompiler

    implementation platform(libs.okhttpBom)
    implementation libs.okhttp

    testImplementation libs.junit
    testRuntimeOnly libs.sargonDesktop
    androidTestImplementation libs.androidXJunit
    androidTestImplementation libs.espresso
}
