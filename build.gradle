import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext {
        minSdk = 27
        compileSdk = 34
        targetSdk = 33
        internalVersionName = "babylon"
        kotlinCompilerExtensionVersion = "1.5.8"
    }
    dependencies {
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.5"
        classpath "org.jacoco:org.jacoco.core:0.8.11"
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.serialization
    alias libs.plugins.kotlin.jvm apply false
    alias libs.plugins.ben.manes.versions
    alias libs.plugins.version.catalog.update
    alias libs.plugins.google.services apply false
    alias libs.plugins.firebase.crashlytics apply false
    alias libs.plugins.hilt apply false
    alias libs.plugins.ksp apply false
}

allprojects {
    apply from: "$rootDir/config/detekt.gradle"
    tasks.withType(Detekt).configureEach {
        exclude("**/test/**")
        exclude("**/generated/**")
        exclude("**/coreapi/**")
        exclude("**/androidTest/**")
    }
}

def isNonStable = { String version ->
    def nonStable = ['ALPHA', 'BETA'].any { it -> version.toUpperCase().contains(it) }
    return nonStable
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
