import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext {
        minSdk = 27
        compileSdk = 33
        targetSdk = 33
        versionName = "1.0.1"
        internalVersionName = "rcnetv3.1"
        kotlinCompilerExtensionVersion = "1.5.3"
    }
    dependencies {
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.48'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.1"
        classpath "org.jacoco:org.jacoco.core:0.8.10"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.9.10"
        classpath "nl.littlerobots.vcu:plugin:0.8.1"
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.1.1' apply false
    id 'com.android.library' version '8.1.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.10' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.10'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10' apply false
    id "com.github.ben-manes.versions" version "0.48.0"
    id "nl.littlerobots.version-catalog-update" version "0.7.0"
}
versionCatalogUpdate {
    sortByKey = false
    versionCatalogs {
        libraries {
            catalogFile = file("./gradle/libraries.versions.toml")
        }
    }
}
allprojects {
    apply from: "$rootDir/config/detekt.gradle"
    tasks.withType(Detekt).configureEach {
        exclude("**/test/**")
        exclude("**/generated/**")
        exclude("**/androidTest/**")
    }
}

def isNonStable = { String version ->
    def nonStable = ['ALPHA', 'BETA'].any { it -> version.toUpperCase().contains(it) }
    return nonStable
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
